QDOC = $$[QT_INSTALL_BINS]/qdoc
QHELPGENERATOR = $$[QT_INSTALL_BINS]/qhelpgenerator

# Note: not $${QT_INSTALL_DOCS}/mer-qdoc-template
# because the SDK prevents installation to /usr/share/doc/
isEmpty(MER_QDOC.template):MER_QDOC.template=/usr/share/mer-qdoc-template
isEmpty(MER_QDOC.style):MER_QDOC.style=offline

QDOCCONF = $${MER_QDOC.template}/$${MER_QDOC.style}.qdocconf
QHELPFILE = html/$${MER_QDOC.project}.qhp
QCHFILE = $${MER_QDOC.project}.qch

ENV += \
    QT_VERSION=$${QT_MAJOR_VERSION}.$${QT_MINOR_VERSION} \
    PROJECT_CONFIG=$${_PRO_FILE_PWD_}/$${MER_QDOC.config} \
    BUILD_DIRECTORY=$${OUT_PWD} \
    TEMPLATE=$${MER_QDOC.template}

qch_docs.target = $${QCHFILE}
qch_docs.depends += FORCE
qch_docs.commands = (env $$ENV $$QDOC $$QDOCCONF) && \
                    ($${QHELPGENERATOR} $${QHELPFILE} -o $${QCHFILE})

generate_docs.target = docs
generate_docs.depends = $${QCHFILE}

docs.depends = $${QCHFILE}
docs.path = $${MER_QDOC.path}
docs.commands = $$QMAKE_INSTALL_FILE $${QCHFILE} $(INSTALL_ROOT)$${docs.path}

QMAKE_EXTRA_TARGETS += qch_docs generate_docs
INSTALLS += docs

OTHER_FILES += $${MER_QDOC.config}
